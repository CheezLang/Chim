use import std.string
use import std.array
use import std.map
use import std.unicode
use import std.mem.std_heap_allocator
use import std.rc

io  :: import std.io
fmt :: import std.fmt

use import glfw
use import glfw_hl

use import editor
use import chim
use import gap_buffer
use import commands

#export_scope

debug_parser :: false

KeyEventType :: enum #copy {
    Char : char
    Space
    Escape
    Enter
    Tab
    Backspace
    Insert
    Delete
    Right
    Left
    Down
    Up
    PageUp
    PageDown
    Home
    End
    CapsLock
    ScrollLock
    NumLock
    PrintScreen
    Pause
    F1
    F2
    F3
    F4
    F5
    F6
    F7
    F8
    F9
    F10
    F11
    F12
    F13
    F14
    F15
    F16
    F17
    F18
    F19
    F20
    F21
    F22
    F23
    F24
    F25
    Kp0
    Kp1
    Kp2
    Kp3
    Kp4
    Kp5
    Kp6
    Kp7
    Kp8
    Kp9
    KpDecimal
    KpDivide
    KpMultiply
    KpSubtract
    KpAdd
    KpEnter
    KpEqual
}

KeyEvent :: struct #copy {
    mods    : GlfwMod
    typ     : KeyEventType
}

ParserState :: enum #copy {
    InProgress
    Success
    Error
}

Parser :: trait {
    state           :: (&Self) -> ParserState;
    advance         :: (&mut Self, event: KeyEvent);
    clone           :: (&Self) -> Rc[Parser];
    build_command   :: (&Self) -> Result[Command, String];
    get_values      :: (&Self, chars: &mut Array[Argument]);
    accepts_numbers :: (&Self) -> bool;
}

CharParser :: struct {
    _state      : ParserState
    character   := '`0'
}

impl CharParser {
    new :: () -> Rc[Parser] {
        return Rc[Parser].new(CharParser(.InProgress))
    }
}

impl Parser for CharParser {
    state :: (&Self) -> ParserState {
        return self._state
    }

    accepts_numbers :: (&Self) -> bool {
        return true
    }

    build_command :: (&Self) -> Result[Command, String] {
        return Err(fmt.format("No command specified"))
    }

    get_values :: (&Self, args: &mut Array[Argument]) {
        match self._state {
            .Success -> {
                args.add(Argument.Char(self.character))
            }
            _ -> {}
        }
    }

    clone :: (&mut Self) -> Rc[Parser] {
        return Self.new()
    }

    advance :: (&mut Self, event: KeyEvent) {
        parser_log("CharParser[].advance({})", [event])

        match self._state {
            .InProgress -> {
                match event.typ {
                    .Char($ch) -> {
                        parser_log("CharParser[].advance({}).InProgress '{}'", [event, ch])
                        if event.mods == .Shift {
                            self.character = ch.to_upper()
                            self._state = .Success
                        } else if event.mods == .None {
                            self.character = ch
                            self._state = .Success
                        } else {
                            self._state = .Error
                        }
                    }

                    .Space -> {
                        if event.mods == .None {
                            self.character = ' '
                            self._state = .Success
                        } else {
                            self._state = .Error
                        }
                    }

                    _ -> {
                        self._state = .Error
                    }
                }
            }
            .Success -> {
                self._state = .Error
            }
            .Error -> {
            }
        }
    }
}

KeyParser :: struct {
    mods    : GlfwMod
    typ     : KeyEventType
    _state  : ParserState
}

impl KeyParser {
    new :: (typ: KeyEventType, mods: GlfwMod = GlfwMod.None) -> Rc[Parser] {
        return Rc[Parser].new(KeyParser(
            mods    = mods
            typ     = typ
            _state  = .InProgress
        ))
    }
}

impl Parser for KeyParser {
    state :: (&Self) -> ParserState {
        return self._state
    }

    accepts_numbers :: (&Self) -> bool {
        return false
    }

    build_command :: (&Self) -> Result[Command, String] {
        return Err(fmt.format("No command specified"))
    }

    get_values :: (&Self, chars: &mut Array[Argument]) {
    }

    clone :: (&mut Self) -> Rc[Parser] {
        return Self.new(self.typ, self.mods)
    }

    advance :: (&mut Self, event: KeyEvent) {
        parser_log("KeyParser[{}, {}].advance({})", [self.mods, self.typ, event])

        match self._state {
            .InProgress -> {
                types_match := match self.typ, event.typ {
                    use KeyEventType
                    Char($c1), Char($c2) -> c1 == c2
                    $t1, $t2 -> t1 == t2
                }
                mods_match := self.mods == event.mods
                parser_log("KeyParser[{}, {}].advance({}): types_match={}, mods_match={}", [self.mods, self.typ, event, types_match, mods_match])
                if types_match and mods_match {
                    self._state = .Success
                } else {
                    self._state = .Error
                }
            }

            .Success -> {
                self._state = .Error
            }

            .Error -> {}
        }
    }
}

CommandKind :: enum {
    BuiltIn : String
    Script  : int
}

impl Clone for CommandKind {
    clone :: (&Self) -> CommandKind {
        return match self {
            .BuiltIn($str) -> .BuiltIn(str.clone())
            $arg -> *arg
        }
    }
}

SequenceParser :: struct {
    command : CommandKind
    args    : Array[Argument]

    count   : int

    parsers : Array[Rc[Parser]]
    current : int
    _state  : ParserState
}

impl SequenceParser {
    new :: (command: CommandKind, arguments: []Argument, parsers: []Rc[Parser]) -> Rc[Parser] {
        mut args := Array[Argument].new()
        for arg in arguments {
            args.add(arg.clone())
        }

        mut new_parsers := Array[Rc[Parser]].new(parsers.length)
        for &p in parsers {
            new_parsers.add(p.get().clone())
        }
        return Rc[Parser].new(SequenceParser(
            command = command
            args    = args
            count   = 0
            parsers = new_parsers
            current = 0
            _state  = .InProgress
        ))
    }

    new2 :: (command: CommandKind, args: Array[Argument], parsers: []Rc[Parser]) -> Rc[Parser] {
        mut new_parsers := Array[Rc[Parser]].new(parsers.length)
        for &p in parsers {
            new_parsers.add(p.get().clone())
        }
        return Rc[Parser].new(SequenceParser(
            command = command
            args    = args
            count   = 0
            parsers = new_parsers
            current = 0
            _state  = .InProgress
        ))
    }
}

impl Parser for SequenceParser {
    state :: (&Self) -> ParserState {
        return self._state
    }

    clone :: (&mut Self) -> Rc[Parser] {
        return Self.new2(self.command.clone(), self.args.clone(), self.parsers[..])
    }

    accepts_numbers :: (&Self) -> bool {
        return if current >= 0 and current < self.parsers.count() then self.parsers[current].get().accepts_numbers() else false
    }

    build_command :: (&Self) -> Result[Command, String] {
        if self._state != .Success {
            return Err(fmt.format("Invalid input"))
        }

        for &parser in self.parsers[..] {
            parser.get().get_values(&mut self.args)
        }

        match &self.command {
            .Script($ref) -> {
                return Ok(Command(self.count, .Script(*ref, self.args.clone())))
            }

            .BuiltIn($name) -> {
                return Command.parse(self.count, name.slice(), self.args[..])
            }

            _ -> @assert(false)
        }
        @assert(false)
        return Err(fmt.format(""))
    }

    get_values :: (&Self, chars: &mut Array[Argument]) {
    }

    advance :: (&mut Self, event: KeyEvent) {
        parser_log("SequenceParser[{}].advance({})", [self.current, event])

        match self._state {
            .InProgress -> {
                if self.current == 0 {
                    match event.mods, event.typ {
                        GlfwMod.None, KeyEventType.Char($ch) if ch.is_digit() -> {
                            if !self.parsers[0].get().accepts_numbers() {
                                self.count = self.count * 10 + int(ch - '0')
                                return
                            }
                        }
                        _ -> {}
                    }
                }

                if self.current >= self.parsers.count() {
                    self._state = .Error
                } else {
                    current := self.parsers[self.current].clone()
                    current.get().advance(event)

                    match current.get().state() {
                        .InProgress -> {
                            self._state = .InProgress
                        }

                        .Success -> {
                            self.current += 1
                            if self.current == self.parsers.count() {
                                self._state = .Success
                            }
                        }

                        .Error -> {
                            self._state = .Error
                        }
                    }
                }
            }

            .Success -> {
                self._state = .Error
            }

            .Error -> {}
        }
    }
}

NormalParser :: struct {
    parsers         : Array[Array[Rc[Parser]]]
    final_parser    : Option[Rc[Parser]]
    _state          : ParserState
}

impl NormalParser {
    new :: (parsers: Array[Array[Rc[Parser]]]) -> Rc[Parser] {
        return Rc[Parser].new(NormalParser(
            parsers         = parsers
            final_parser    = .None
            _state          = .InProgress
        ))
    }
}

impl Parser for NormalParser {
    state :: (&Self) -> ParserState {
        return self._state
    }

    accepts_numbers :: (&Self) -> bool {
        for &parsers in self.parsers[..] {
            for &parser in parsers[..] {
                if parser.get().accepts_numbers() then {
                    return true
                }
            }
        }

        return false
    }

    clone :: (&mut Self) -> Rc[Parser] {
        mut parsers := Array[Array[Rc[Parser]]].new()
        for &p in self.parsers[..] {
            parsers.add(Parsers.clone_array(p[..]))
        }
        return Self.new(parsers)
    }

    build_command :: (&Self) -> Result[Command, String] {
        if self._state != .Success {
            return Err(fmt.format("Multiple commands matched the input", []))
        }

        @assert(self.final_parser.is_some())

        return self.final_parser.unwrap_ref().get().build_command()
    }

    get_values :: (&Self, chars: &mut Array[Argument]) {
    }

    advance :: (&mut Self, event: KeyEvent) {
        match self._state {
            .InProgress -> {
                parser_log("NormalParser.advance({})", [event])

                for &mut parsers in self.parsers.slice_mut() {
                    for {0 .. parsers.count()}.reverse() {
                        p := parsers[it].get_mut()
                        p.advance(event)
                        match p.state() {
                            .InProgress -> {}
                            .Success -> {}
                            .Error -> {
                                parsers.remove_at(it)
                            }
                        }
                    }
                }

                mut in_progress := false
                for &parsers in self.parsers[..] #label outer_loop {
                    for &p in parsers[..] {
                        if p.get().state() == .Success {
                            self.final_parser = Some(p.clone())
                            in_progress = false
                            break outer_loop
                        }
                    }
                    if parsers.count() > 0 {
                        in_progress = true
                    }
                }

                if in_progress {
                    self._state = .InProgress
                } else if self.final_parser.is_none() {
                    self._state = .Error
                } else {
                    self._state = .Success
                }
            }

            .Success -> {
                self._state = .Error
            }

            .Error -> {}
        }
    }
}

impl Chim {
    key_input_parser :: (self: ^Chim) {
    }

    parse :: (&mut Self, mut event_index: int) {
    }

    handle_key_event :: (&mut Self, event: KeyEvent) {
        // io.formatln("===========================")
        // io.formatln("{}", [&event])

        self.key_events.add(event)

        if self.current_parser.is_none() {
            parser_log("handle_key_event: creating new parser")

            active_language := self.active_view.get().buffer.get().language.unwrap_or("")

            mut parsers := Array[Array[Rc[Parser]]].new()

            if active_language != "" {
                match self.language_mappings.get(active_language) {
                    Some($mappings) -> {
                        match mappings.get(current_mode) {
                            Some($p) -> {
                                normal, move := p.clone()
                                parsers.add(normal)
                                parsers.add(move)
                            }

                            None -> {
                                // io.formatln("{}: No keybindings for mode '{}' specified", [active_language, current_mode])
                            }
                        }
                    }

                    None -> {}
                }
            }

            match self.language_mappings.get("") {
                Some($mappings) -> {
                    match mappings.get(current_mode) {
                        Some($p) -> {
                            normal, move := p.clone()
                            parsers.add(normal)
                            parsers.add(move)
                        }

                        None -> {
                            // io.formatln("No keybindings for mode '{}' specified", [current_mode])
                        }
                    }
                }

                None -> {}
            }

            self.current_parser = Some(NormalParser.new(parsers))
        }

        @assert(self.current_parser.is_some())

        parser := self.current_parser.unwrap_ref().clone()
        parser.get().advance(event)
        match parser.get().state() {
            .InProgress -> parser_log("handle_key_event: parser still in progress")
            .Success -> {
                parser_log("handle_key_event: parser succeeded")
                command := parser.get().build_command()
                // io.formatln("command: {}", [&command])

                match command {
                    Ok($command) -> {
                        self.commands.push(command)
                    }
                    Err($err) -> {
                        io.formatln("Failed to build command for input: {}", [err.slice()])
                    }
                }

                self.current_parser = .None
                self.key_events.clear()
            }
            .Error -> {
                parser_log("handle_key_event: parser failed")
                self.current_parser = .None
                self.key_events.clear()
            }
        }
    }

    on_key :: (&mut Self, key: GlfwKey, scancode: i32, action: GlfwAction, mods: GlfwMod) {
        // io.formatln("{}, {}, {}, {}", [key, scancode, action, mods])
        match action, key {
            use GlfwKey
            use GlfwAction
            use GlfwMod

            Press, LeftShift        -> { current_mods_l = current_mods_l or Shift }
            Release, LeftShift      -> { current_mods_l = current_mods_l and !Shift }
            Press, RightShift       -> { current_mods_r = current_mods_r or Shift }
            Release, RightShift     -> { current_mods_r = current_mods_r and !Shift }

            Press, LeftControl      -> { current_mods_l = current_mods_l or Control }
            Release, LeftControl    -> { current_mods_l = current_mods_l and !Control }
            Press, RightControl     -> { current_mods_r = current_mods_r or Control }
            Release, RightControl   -> { current_mods_r = current_mods_r and !Control }

            Press, LeftAlt          -> { current_mods_l = current_mods_l or Alt }
            Release, LeftAlt        -> { current_mods_l = current_mods_l and !Alt }
            Press, RightAlt         -> { current_mods_r = current_mods_r or (Alt or Control) }
            Release, RightAlt       -> { current_mods_r = current_mods_r and !(Alt or Control) }

            Press, LeftSuper        -> { current_mods_l = current_mods_l or Super }
            Release, LeftSuper      -> { current_mods_l = current_mods_l and !Super }
            Press, RightSuper       -> { current_mods_r = current_mods_r or Super }
            Release, RightSuper     -> { current_mods_r = current_mods_r and !Super }

            Press, F1               -> {
                self.show_debug_window = !self.show_debug_window
                return
            }

            Press, F2               -> {
                self.show_demo_window = !self.show_demo_window
                return
            }

            _ -> {}
        }

        current_mods = current_mods_l or current_mods_r

        if !self.has_focus {
            return
        }

        if action == .Press or action == .Repeat {
            if mods != .None and mods != .Shift {
                key_name_c := glfwGetKeyName(key, scancode)
                if key_name_c != null {
                    key_name := string.from_cstring(key_name_c)
                    ch, _ := Utf8.decode(key_name.bytes)
                    if ch == 'e' and (GlfwMod.Control or GlfwMod.Alt is in mods) {
                        self.handle_key_event(KeyEvent(mods and !(GlfwMod.Control or GlfwMod.Alt), KeyEventType.Char('€')))
                        return
                    }
                    if ch == 'q' and (GlfwMod.Control or GlfwMod.Alt is in mods) {
                        self.handle_key_event(KeyEvent(mods and !(GlfwMod.Control or GlfwMod.Alt), KeyEventType.Char('@')))
                        return
                    }
                    if ch == 'ß' and (GlfwMod.Control or GlfwMod.Alt is in mods) {
                        self.handle_key_event(KeyEvent(mods and !(GlfwMod.Control or GlfwMod.Alt), KeyEventType.Char('\')))
                        return
                    }
                    if ch.is_alpha() {
                        self.handle_key_event(KeyEvent(mods, KeyEventType.Char(ch)))
                        return
                    }
                }
            }

            mut mods := self.current_mods
            typ := match key {
                GlfwKey._0 if mods == .Control -> KeyEventType.Char('0')
                GlfwKey._1 if mods == .Control -> KeyEventType.Char('1')
                GlfwKey._2 if mods == .Control -> KeyEventType.Char('2')
                GlfwKey._3 if mods == .Control -> KeyEventType.Char('3')
                GlfwKey._4 if mods == .Control -> KeyEventType.Char('4')
                GlfwKey._5 if mods == .Control -> KeyEventType.Char('5')
                GlfwKey._6 if mods == .Control -> KeyEventType.Char('6')
                GlfwKey._7 if mods == .Control -> KeyEventType.Char('7')
                GlfwKey._8 if mods == .Control -> KeyEventType.Char('8')
                GlfwKey._9 if mods == .Control -> KeyEventType.Char('9')

                GlfwKey.Space if self.current_mods != .None -> KeyEventType.Space
                GlfwKey.Escape -> KeyEventType.Escape
                GlfwKey.Enter -> KeyEventType.Enter
                GlfwKey.Tab -> KeyEventType.Tab
                GlfwKey.Backspace -> KeyEventType.Backspace
                GlfwKey.Insert -> KeyEventType.Insert
                GlfwKey.Delete -> KeyEventType.Delete
                GlfwKey.Right -> KeyEventType.Right
                GlfwKey.Left -> KeyEventType.Left
                GlfwKey.Down -> KeyEventType.Down
                GlfwKey.Up -> KeyEventType.Up
                GlfwKey.PageUp -> KeyEventType.PageUp
                GlfwKey.PageDown -> KeyEventType.PageDown
                GlfwKey.Home -> KeyEventType.Home
                GlfwKey.End -> KeyEventType.End
                GlfwKey.CapsLock -> KeyEventType.CapsLock
                GlfwKey.ScrollLock -> KeyEventType.ScrollLock
                GlfwKey.NumLock -> KeyEventType.NumLock
                GlfwKey.PrintScreen -> KeyEventType.PrintScreen
                GlfwKey.Pause -> KeyEventType.Pause
                GlfwKey.F1 -> KeyEventType.F1
                GlfwKey.F2 -> KeyEventType.F2
                GlfwKey.F3 -> KeyEventType.F3
                GlfwKey.F4 -> KeyEventType.F4
                GlfwKey.F5 -> KeyEventType.F5
                GlfwKey.F6 -> KeyEventType.F6
                GlfwKey.F7 -> KeyEventType.F7
                GlfwKey.F8 -> KeyEventType.F8
                GlfwKey.F9 -> KeyEventType.F9
                GlfwKey.F10 -> KeyEventType.F10
                GlfwKey.F11 -> KeyEventType.F11
                GlfwKey.F12 -> KeyEventType.F12
                GlfwKey.F13 -> KeyEventType.F13
                GlfwKey.F14 -> KeyEventType.F14
                GlfwKey.F15 -> KeyEventType.F15
                GlfwKey.F16 -> KeyEventType.F16
                GlfwKey.F17 -> KeyEventType.F17
                GlfwKey.F18 -> KeyEventType.F18
                GlfwKey.F19 -> KeyEventType.F19
                GlfwKey.F20 -> KeyEventType.F20
                GlfwKey.F21 -> KeyEventType.F21
                GlfwKey.F22 -> KeyEventType.F22
                GlfwKey.F23 -> KeyEventType.F23
                GlfwKey.F24 -> KeyEventType.F24
                GlfwKey.F25 -> KeyEventType.F25
                GlfwKey.Kp0 -> KeyEventType.Kp0
                GlfwKey.Kp1 -> KeyEventType.Kp1
                GlfwKey.Kp2 -> KeyEventType.Kp2
                GlfwKey.Kp3 -> KeyEventType.Kp3
                GlfwKey.Kp4 -> KeyEventType.Kp4
                GlfwKey.Kp5 -> KeyEventType.Kp5
                GlfwKey.Kp6 -> KeyEventType.Kp6
                GlfwKey.Kp7 -> KeyEventType.Kp7
                GlfwKey.Kp8 -> KeyEventType.Kp8
                GlfwKey.Kp9 -> KeyEventType.Kp9
                GlfwKey.KpDecimal -> KeyEventType.KpDecimal
                GlfwKey.KpDivide -> KeyEventType.KpDivide
                GlfwKey.KpMultiply -> KeyEventType.KpMultiply
                GlfwKey.KpSubtract -> KeyEventType.KpSubtract
                GlfwKey.KpAdd -> KeyEventType.KpAdd
                GlfwKey.KpEnter -> KeyEventType.KpEnter
                GlfwKey.KpEqual -> KeyEventType.KpEqual

                _ -> {
                    return
                }
            }

            self.handle_key_event(KeyEvent(mods, typ))
        }
    }

    on_char :: (&mut Self, c: char) {
        if !self.has_focus {
            return
        }

        if c == '@' or c == '€' or c == '\' or (c == ' ' and self.current_mods != .None) {
            return
        }

        if c == ' ' {
            self.handle_key_event(KeyEvent(self.current_mods, KeyEventType.Space))
            return
        }

        mods, c := if c.is_alpha() or c.is_digit() {
            if (c >= 'A' and c <= 'Z') or (c == 'Ä' or c == 'Ö' or c == 'Ü') {
                self.current_mods or GlfwMod.Shift, c.to_lower()
            } else {
                self.current_mods, c
            }
        } else {
            GlfwMod.None, c
        }

        self.handle_key_event(KeyEvent(mods, KeyEventType.Char(c)))
    }
}

parser_log :: (format: string, args: []^any = []) {
    if const debug_parser {
        io.formatln(format, args)
    }
}